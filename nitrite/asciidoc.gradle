/*
 *
 * Copyright 2017-2018 Nitrite author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

def generatedSources = "$buildDir/generated-src/delombok/java"

apply plugin: 'org.asciidoctor.convert'

configurations {
    asciidoclet
}

dependencies {
    asciidoclet "org.asciidoctor:asciidoclet:$asciidoctorVersion"
}

asciidoctorj {
    version = "$asciidoctorVersion"
}

asciidoctor {
    backends = ['html5']
    attributes  \
            'endpoint-url': 'http://nitrite.dizitart.org',
            'source-highlighter': 'coderay',
            'toc': 'left',
            'icons': 'font',
            'setanchors': '',
            'idprefix': '',
            'idseparator': '-',
            'docinfo1': ''
}

javadoc {
    setDependsOn(['delombok', 'copyIcons'])
    source = "$generatedSources"
    exclude("**/overview.adoc")
    options.docletpath = configurations.asciidoclet.files as List
    options.doclet = 'org.asciidoctor.Asciidoclet'
    options.overview = "$generatedSources/overview.adoc"
    options.addStringOption "-base-dir", "${projectDir}"
    options.addStringOption "doctitle", "${project.name.capitalize()} ${project.version} API"
    options.addStringOption "windowtitle", "${project.name.capitalize()} ${project.version} API"
    options.addStringOption "-attribute",
            "icons," +
                    "iconsdir=.," +
                    "name=${project.name}," +
                    "version=${project.version}," +
                    "title-link=http://nitrite.dizitart.org[${project.name} ${project.version}]"
}

task copyIcons(type: Copy) {
    from "${projectDir}/src/docs/icons"
    into "${buildDir}/docs/javadoc"
}